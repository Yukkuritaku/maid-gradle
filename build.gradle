plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    //id 'com.gradle.plugin-publish' version '1.2.1'
}

group = 'io.github.yukkuritaku'
def archiveName = 'maid-gradle'
version = '0.4'

configurations {
    loom
    compileOnly.extendsFrom(loom)
}

repositories {
    mavenCentral()
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(17)
    it.options.encoding = 'UTF-8'
}

java {
    withSourcesJar()
    withJavadocJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar{
    manifest.attributes(
            "Implementation-Version": version
    )

}

dependencies {
    implementation gradleApi()
    loom 'net.fabricmc:fabric-loom:1.3.9'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-compress
    implementation 'org.apache.commons:commons-compress:1.24.0'
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation(gradleTestKit())
    testImplementation('org.spockframework:spock-core:2.3-groovy-3.0') {
        exclude module: 'groovy-all'
    }
}

gradlePlugin {
    website = 'https://github.com/Yukkuritaku/maid-gradle'
    vcsUrl = 'https://github.com/Yukkuritaku/maid-gradle.git'
    plugins {
        maidGradle {
            id = 'io.github.yukkuritaku.maid-gradle'
            displayName = "Maid-Gradle"
            description = "Building LittleMaidModel for Minecraft Fabric Modding developers."
            implementationClass = 'io.github.yukkuritaku.maidgradle.loom.bootstrap.MaidGradlePluginBootstrap'
            tags.set(['littlemaid', 'modding'])
        }
    }
}

test {
    useJUnitPlatform()
}

publishing {
    publications{
        releases(MavenPublication){
            from components.java
            groupId = "${group}"
            artifactId = "${archiveName}"
            version = "${version}"
        }
    }
}

