plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group = 'com.github.yukkuritaku'
version = '0.1-SNAPSHOT'

configurations {
    loom
    compileOnly.extendsFrom(loom)
}

repositories {
    mavenCentral()
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
}

dependencies {
    implementation gradleApi()
    loom 'net.fabricmc:fabric-loom:1.3.9'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

gradlePlugin {
    plugins {
        maidGradle {
            id = 'maid-gradle'
            implementationClass = 'com.github.yukkuritaku.maidgradle.bootstrap.MaidGradlePluginBootstrap'
        }
    }
}

test {
    useJUnitPlatform()
}

publishing {
    file(".env").readLines().each {
        def (key, value) = it.tokenize('=')
        System.setProperty(key, value)
    }
    repositories {
        maven {
            name = "GithubRepository"
            url = uri("https://maven.pkg.github.com/Yukkuritaku/maid-gradle")
            credentials {
                username = System.getProperty("gpr_user")
                password = System.getProperty("gpr_key")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = group
            artifactId = name
            version = version
            from(components.java)
        }
    }
}